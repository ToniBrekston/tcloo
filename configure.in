dnl#
dnl# When changing the version number, you *must* change the following:
dnl#	generic/tclOO.h, tests/oo.test, tests/ooNext2.test
dnl#
dnl# You should also change the following:
dnl#	README.txt, win/TclOO.rc
dnl#
AC_INIT([TclOO],[1.0.1])
AC_CONFIG_AUX_DIR(tclconfig)
AC_CONFIG_HEADERS(config.h)

TEA_INIT([3.9])
TEA_PATH_TCLCONFIG
TEA_LOAD_TCLCONFIG
TEA_PREFIX
TEA_SETUP_COMPILER
AC_C_INLINE
TEA_ADD_SOURCES([
	tclOO.c tclOOBasic.c tclOOCall.c tclOODefineCmds.c tclOOInfo.c
	tclOOMethod.c tclOOStubInit.c])
TEA_ADD_STUB_SOURCES([tclOOStubLib.c])
TEA_ADD_HEADERS([generic/tclOO.h generic/tclOODecls.h])
TEAX_ADD_PRIVATE_HEADERS([generic/tclOOInt.h generic/tclOOIntDecls.h])
TEAX_INCLUDE_DIR([.])
TEAX_INCLUDE_DIR([${srcdir}/generic])
TEA_PRIVATE_TCL_HEADERS
TEA_ENABLE_THREADS
TEA_ENABLE_SHARED
TEA_CONFIG_CFLAGS
dnl TEAX_SUBST_RESOURCE(PACKAGE_NAME PKG_LIB_FILE PACKAGE_VERSION)
TEA_ENABLE_SYMBOLS
AC_CHECK_TYPE([intptr_t], [
    AC_DEFINE([HAVE_INTPTR_T], 1, [Do we have the intptr_t type?])], [
    AC_CACHE_CHECK([for pointer-size signed integer type], tcl_cv_intptr_t, [
    for tcl_cv_intptr_t in "int" "long" "long long" none; do
	if test "$tcl_cv_intptr_t" != none; then
	    AC_COMPILE_IFELSE([AC_LANG_BOOL_COMPILE_TRY([AC_INCLUDES_DEFAULT],
		    [[sizeof (void *) <= sizeof ($tcl_cv_intptr_t)]])],
		[tcl_ok=yes], [tcl_ok=no])
	    test "$tcl_ok" = yes && break; fi
    done])
    if test "$tcl_cv_intptr_t" != none; then
	AC_DEFINE_UNQUOTED([intptr_t], [$tcl_cv_intptr_t], [Signed integer
	   type wide enough to hold a pointer.])
    fi
])

TEA_MAKE_LIB
dnl TEAX_VC_MANIFEST

TEAX_LAPPEND(CLEANFILES, pkgIndex.tcl)
AC_SUBST(CLEANFILES)
AC_DEFINE(USE_TCL_STUBS,[1],[Should always be 1])

TEA_PROG_TCLSH
CONFIGURE_OUTPUTS="Makefile tclooConfig.sh config.cache config.log config.status"
AC_SUBST(CONFIGURE_OUTPUTS)

TEAX_SDX

TEAX_CONFIG_INCLUDE_LINE(TCLOO_INCLUDE_SPEC,	     [${includedir}])
TEAX_CONFIG_INCLUDE_LINE(TCLOO_PRIVATE_INCLUDE_SPEC, [${includedir}])
TEAX_CONFIG_LINK_LINE(TCLOO_LIB_SPEC,
	[${libdir}/${PACKAGE_NAME}${PACKAGE_VERSION}], [${PACKAGE_NAME}])
TEAX_CONFIG_LINK_LINE(TCLOO_STUB_LIB_SPEC,
	[${libdir}/${PACKAGE_NAME}${PACKAGE_VERSION}], [${PACKAGE_NAME}stub])

AC_CONFIG_FILES(Makefile tclooConfig.sh)
AC_OUTPUT
